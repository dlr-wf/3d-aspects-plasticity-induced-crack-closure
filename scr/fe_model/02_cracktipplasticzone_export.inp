/POST1
ALLSEL

! variables
*DIM,res,TABLE,steps,5
boundary = 0.9

i = 0
*DO,ls,5,number_loadsteps,3
i = i+1
res(i,1) = i

!plastic zone
SET,ls-3
ALLSEL
ETABLE,eppltab,NL,EPEQ
*VGET,epp_1,ELEM,,ETAB,eppltab

SET,ls
ALLSEL
ETABLE,eppltab,NL,EPEQ
*VGET,epp_2,ELEM,,ETAB,eppltab

*Get,maxel,ELEM,0,NUM,MAXD
ALLSEL  
ESEL,NONE   
*DO,ii,1,maxel,1
	*IF,epp_1(ii,1),NE,epp_2(ii,1),THEN   
		ESEL,A,ELEM,,ii 
	*ENDIF  
*ENDDO 

ESEL,U,CENT,X,0,a-offset+((i)*esi)	
*Get,pzel,ELEM,0,NUM,MAXD
CM,plasticzone,ELEM

!determine plastic energy boundary
ETAB,pzvolume,VOLU
ETAB,pzenergy_dens,SEND,PLASTIC
SMULT,pzenergy,pzenergy_dens,pzvolume,1,1
ssum
*GET,pzenergy_sum,ssum,pzenergy
*GET,pzvolume_sum,ssum,pzvolume
pzenergy_bon = boundary*pzenergy_sum
*VGET,pzenergy_el,ELEM,,ETAB,pzenergy
res(i,2) = pzenergy_sum
res(i,3) = pzvolume_sum

!damage zone calculation
tempboundary = 0.015
incboundary = 0.0002
itermax = tempboundary/incboundary

*DO,jj,1,itermax,1
	
	ALLSEL
	CMSEL,S,plasticzone,ELEM
	*DO,kk,1,pzel,1
		*IF,pzenergy_el(kk),LT,tempboundary,THEN
			ESEL,U,ELEM,,kk
		*ENDIF
	*ENDDO
	
	*GET,pz_temp,ELEM,,COUNT
	
	ETAB,pzv,VOLU
	ETAB,pzed,SEND,PLASTIC
	SMULT,pze_temp,pzed,pzv,1,1
	ssum
	*GET,pze_sum_temp,ssum,pze_temp
	*GET,pzv_sum_temp,ssum,pzv
	
	*IF,pze_sum_temp,LT,pzenergy_bon,THEN
		tempboundary = tempboundary - incboundary
	*ELSE
		*EXIT
	*ENDIF
	
*ENDDO

res(i,4) = pze_sum_temp
res(i,5) = pzv_sum_temp

*ENDDO

*CFOPEN,R%R%.K%K%.t%t*2%.CTPlasticzone_,txt,,
*VWRITE,'cycle,','upl,','volu,','upl90,','volu90'
(A21,A21,A21,A21,A21)
*CFCLOS

*CFOPEN,R%R%.K%K%.t%t*2%.CTPlasticzone_,txt,,append
*VWRITE,res(1,1),res(1,2),res(1,3),res(1,4),res(1,5)
(F20.10,',',3(F20.10,',',)F20.10)
*CFCLOS




