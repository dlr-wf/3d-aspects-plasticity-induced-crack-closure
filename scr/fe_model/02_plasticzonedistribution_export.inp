/POST1
ALLSEL

! variables
*DIM,res,TABLE,t/esd,5
boundary = 0.9

!plastic zone
SET,number_loadsteps-4
ALLSEL
ETABLE,eppltab,NL,EPEQ
*VGET,epp_1,ELEM,,ETAB,eppltab

SET,number_loadsteps-1
ALLSEL
ETABLE,eppltab,NL,EPEQ
*VGET,epp_2,ELEM,,ETAB,eppltab

*Get,maxel,ELEM,0,NUM,MAXD
ALLSEL  
ESEL,NONE   
*DO,ii,1,maxel,1
	*IF,epp_1(ii,1),NE,epp_2(ii,1),THEN   
		ESEL,A,ELEM,,ii 
	*ENDIF  
*ENDDO 

ESEL,U,CENT,X,0,a-offset+((steps-2)*esi)
CM,plasticzone,ELEM
*Get,pzel,ELEM,0,NUM,MAXD

!determine plastic energy boundary
ETAB,pzvolume,VOLU
ETAB,pzenergy_dens,SEND,PLASTIC
SMULT,pzenergy,pzenergy_dens,pzvolume,1,1
ssum
*GET,pzenergy_sum,ssum,pzenergy
*GET,pzvolume_sum,ssum,pzvolume
pzenergy_bon = boundary*pzenergy_sum
*VGET,pzenergy_el,ELEM,,ETAB,pzenergy

!damage zone calculation
tempboundary = 0.015
incboundary = 0.0001
itermax = tempboundary/incboundary
loopcontrol = 1

*DO,jj,1,itermax,1
	
	ALLSEL
	CMSEL,S,plasticzone,ELEM
	*DO,kk,1,pzel,1
		*IF,pzenergy_el(kk),LT,tempboundary,THEN
			ESEL,U,ELEM,,kk
		*ENDIF
	*ENDDO
	
	*GET,pz_temp,ELEM,,COUNT
	
	ETAB,pzv,VOLU
	ETAB,pzed,SEND,PLASTIC
	SMULT,pze_temp,pzed,pzv,1,1
	ssum
	*GET,pze_sum_temp,ssum,pze_temp
	*GET,pzv_sum_temp,ssum,pzv
	
	*IF,pze_sum_temp,LT,pzenergy_bon,THEN
		loopcontrol = 1
		tempboundary = tempboundary - incboundary
	*ELSE
		*EXIT
	*ENDIF
	
*ENDDO

CM,damagezone,ELEM

*DO,ii,1,t/esd,1
	
	res(ii,1) = (esd)*ii - esd/2

	CMSEL,S,plasticzone,ELEM
	ESEL,R,CENT,Z,(ii-1)*esd, ii*esd
	
	ETAB,pzv_e,VOLU
	ETAB,pzed_e,SEND,PLASTIC
	SMULT,pze_te,pzed_e,pzv_e,1,1
	ssum
	*GET,pze_sum_e,ssum,pze_te
	*GET,pzv_sum_e,ssum,pzv
	res(ii,2) = pze_sum_e
	res(ii,3) = pzv_sum_e
	
	CMSEL,S,damagezone,ELEM
	ESEL,R,CENT,Z,(ii-1)*esd, ii*esd
	
	ETAB,pzv_e,VOLU
	ETAB,pzed_e,SEND,PLASTIC
	SMULT,pze_te,pzed_e,pzv_e,1,1
	ssum
	*GET,pze_sum_e,ssum,pze_te
	*GET,pzv_sum_e,ssum,pzv
	res(ii,4) = pze_sum_e
	res(ii,5) = pzv_sum_e
	
*ENDDO

*CFOPEN,R%R%.K%K%.t%t*2%.plasticzonedistribution_,txt,,
*VWRITE,'t,','upl,','volu,','upl90,','volu90'
(A21,A21,A21,A21,A21)
*CFCLOS

*CFOPEN,R%R%.K%K%.t%t*2%.plasticzonedistribution_,txt,,append
*VWRITE,res(1,1),res(1,2),res(1,3),res(1,4),res(1,5)
(F20.10,',',3(F20.10,',',)F20.10)
*CFCLOS
	
	
	
