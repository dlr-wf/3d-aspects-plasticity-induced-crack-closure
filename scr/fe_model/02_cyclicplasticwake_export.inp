/POST1
ALLSEL

! variables
*DIM,res,TABLE,steps,13

i = 0
*DO,ls,5,number_loadsteps,3
i = i+1
res(i,1) = i

!plastic zone
SET,ls-3
ALLSEL
ETABLE,eppltab,NL,EPEQ
*VGET,epp_1,ELEM,,ETAB,eppltab

SET,ls
ALLSEL
ETABLE,eppltab,NL,EPEQ
*VGET,epp_2,ELEM,,ETAB,eppltab

*Get,maxel,ELEM,0,NUM,MAXD
ALLSEL  
ESEL,NONE   
*DO,ii,1,maxel,1
	*IF,epp_1(ii,1),NE,epp_2(ii,1),THEN   
		ESEL,A,ELEM,,ii 
	*ENDIF  
*ENDDO 

ESEL,U,CENT,X,a-offset+((i-2)*esi),b	
*GET,pzel,ELEM,,COUNT
CM,plasticzone,ELEM

!determine plastic energy boundary
ETAB,pzvolume,VOLU
ETAB,pzenergy_dens,SEND,PLASTIC
SMULT,pzenergy,pzenergy_dens,pzvolume,1,1
ssum
*GET,pzenergy_sum,ssum,pzenergy
*GET,pzvolume_sum,ssum,pzvolume
pzenergy_bon = boundary*pzenergy_sum
*VGET,pzenergy_el,ELEM,,ETAB,pzenergy
res(i,2) = pzenergy_sum
res(i,3) = pzvolume_sum

b_sum = 0
*DO,ii,1,i,1

	CMSEL,S,plasticzone,ELEM
	ESEL,R,CENT,X,a-offset+((ii)*esi), a-offset+((ii-1)*esi)
	
	ETAB,pz_b,CENT,Z
	ESORT,ETAB,pz_b,0
	*GET,pz_b,SORT,0,MAX
	b_sum = b_sum + pz_b
*ENDDO

res(i,4) = b_sum/i
res(i,5) = pzel*esi*esd
res(i,6) = i*esi*t

SET,ls
CMSEL,S,plasticzone,ELEM
ETAB,pzvol,VOLU
ETAB,pzen_dens,SEND,PLASTIC
SMULT,pzegy,pzen_dens,pzvol,1,1

ETAB,epplx_1,EPPL,X
ETAB,epply_1,EPPL,Y
ETAB,epplz_1,EPPL,Z
ETAB,epplxy_1,EPPL,XY
ETAB,epplyz_1,EPPL,YZ
ETAB,epplxz_1,EPPL,XZ

ssum
*GET,upl_1,ssum,pzegy
*GET,eplx_1,ssum,epplx_1
*GET,eply_1,ssum,epply_1
*GET,eplz_1,ssum,epplz_1
*GET,eplxy_1,ssum,epplxy_1
*GET,eplyz_1,ssum,epplyz_1
*GET,eplxz_1,ssum,epplxz_1

SET,ls-2
ETAB,pzvol,VOLU
ETAB,pzen_dens,SEND,PLASTIC
SMULT,pzegy,pzen_dens,pzvol,1,1

ETAB,epplx_1,EPPL,X
ETAB,epply_1,EPPL,Y
ETAB,epplz_1,EPPL,Z
ETAB,epplxy_1,EPPL,XY
ETAB,epplyz_1,EPPL,YZ
ETAB,epplxz_1,EPPL,XZ

ssum
*GET,upl_2,ssum,pzegy
*GET,eplx_2,ssum,epplx_1
*GET,eply_2,ssum,epply_1
*GET,eplz_2,ssum,epplz_1
*GET,eplxy_2,ssum,epplxy_1
*GET,eplyz_2,ssum,epplyz_1
*GET,eplxz_2,ssum,epplxz_1

res(i,7) = upl_1-upl_2
res(i,8) = eplx_1-eplx_2
res(i,9) = eply_1-eply_2
res(i,10) = eplz_1-eplz_2
res(i,11) = eplxy_1-eplxy_2
res(i,12) = eplyz_1-eplyz_2
res(i,13) = eplxz_1-eplxz_2

*ENDDO

*CFOPEN,R%R%.K%K%.t%t*2%.cyclicplasticwake_,txt,,
*VWRITE,'cycle,','upl,','volu,','b,','Apz,','Ages,','dupl,','epx,','epy,','epz,','epxy,','epyz,','epxz'
(A21,A21,A21,A21,A21,A21,A21,A21,A21,A21,A21,A21,A21)
*CFCLOS

*CFOPEN,R%R%.K%K%.t%t*2%.cyclicplasticwake_,txt,,append
*VWRITE,res(1,1),res(1,2),res(1,3),res(1,4),res(1,5),res(1,6),res(1,7),res(1,8),res(1,9),res(1,10),res(1,11),res(1,12),res(1,13)
(F20.10,',',11(F20.10,',',)F20.10)
*CFCLOS




